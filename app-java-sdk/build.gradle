apply plugin: 'java-library'
apply plugin: 'com.novoda.bintray-release'


ext {
    PUBLISH_GROUP_ID = "com.whatspos.sdk"        //项目包名
    PUBLISH_ARTIFACT_ID = 'paxstore-3rd-app-java-sdk'            //项目名
    PUBLISH_VERSION = '8.1.0'                    //版本号
}

apply from: "${rootProject.projectDir}/publish-mavencentral.gradle"



dependencies {
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'commons-io:commons-io:2.4'
    implementation 'dom4j:dom4j:1.6.1'
    implementation 'org.slf4j:slf4j-api:1.7.12'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
}


task generateSdkVersion {
    inputs.property "version", "${version}"
    outputs.dir "$buildDir/generated"
    doFirst {
        def versionFile = file("$buildDir/generated/com/pax/market/api/sdk/java/Version.java")
        versionFile.parentFile.mkdirs()
        versionFile.text =
                """
package com.pax.market.api.sdk.java;

public class Version {
	public static String getVersion() {
		return "${version}";
	}
}
"""
    }
}
compileJava.dependsOn generateSdkVersion
compileJava.source generateSdkVersion.outputs.files, sourceSets.main.java


sourceCompatibility = "1.8"
targetCompatibility = "1.8"

publish {
    userOrg = 'paxstore-support'
    repoName = 'paxstore'
    groupId = 'com.pax.market'
    artifactId = 'paxstore-3rd-app-java-sdk'
    publishVersion = "${version}"
    desc = 'Basic java APIs for third party developers to integrate their apps on PAXSTORE.'
    website = 'https://github.com/PAXSTORE/paxstore-3rd-app-java-sdk'
    licences = ["Apache-2.0"]
}
